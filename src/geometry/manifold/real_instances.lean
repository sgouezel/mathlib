/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import geometry.manifold.smooth_manifold_with_corners
import linear_algebra.finite_dimensional
import analysis.normed_space.real_inner_product

/-!
# Constructing examples of manifolds over ‚Ñù

We introduce the necessary bits to be able to define manifolds modelled over `‚Ñù^n`, boundaryless
or with boundary or with corners. As a concrete example, we construct explicitly the manifold with
boundary structure on the real interval `[x, y]`.

More specifically, we introduce
* `model_with_corners ‚Ñù (euclidean_space n) (euclidean_half_space n)` for the model space used
to define `n`-dimensional real manifolds with boundary
* `model_with_corners ‚Ñù (euclidean_space n) (euclidean_quadrant n)` for the model space used
to define `n`-dimensional real manifolds with corners

## Implementation notes

The manifold structure on the interval `[x, y] = Icc x y` requires the assumption `x < y` as a
typeclass. We provide it as `[fact (x < y)]`.
-/

noncomputable theory
open set

/--
The half-space in `‚Ñù^n`, used to model manifolds with boundary. We only define it when
`1 ‚â§ n`, as the definition only makes sense in this case.
-/
def euclidean_half_space (n : ‚Ñï) [has_zero (fin n)] : Type :=
{x : euclidean_space (fin n) // 0 ‚â§ x 0}

/--
The quadrant in `‚Ñù^n`, used to model manifolds with corners, made of all vectors with nonnegative
coordinates.
-/
def euclidean_quadrant (n : ‚Ñï) : Type := {x : euclidean_space (fin n) // ‚àÄi:fin n, 0 ‚â§ x i}

section
/- Register class instances for euclidean space and half-space and quadrant, that can not be noticed
without the following reducibility attribute (which is only set in this section). -/
local attribute [reducible] euclidean_half_space euclidean_quadrant
variable {n : ‚Ñï}

instance [has_zero (fin n)] : topological_space (euclidean_half_space n) := by apply_instance
instance : topological_space (euclidean_quadrant n) := by apply_instance
instance [has_zero (fin n)] : inhabited (euclidean_half_space n) := ‚ü®‚ü®0, le_refl _‚ü©‚ü©
instance : inhabited (euclidean_quadrant n) := ‚ü®‚ü®0, Œª i, le_refl _‚ü©‚ü©

lemma range_half_space (n : ‚Ñï) [has_zero (fin n)] :
  range (Œªx : euclidean_half_space n, x.val) = {y | 0 ‚â§ y 0 } :=
by simp

lemma range_quadrant (n : ‚Ñï) :
  range (Œªx : euclidean_quadrant n, x.val) = {y | ‚àÄi:fin n, 0 ‚â§ y i } :=
by simp

end

/--
Definition of the model with corners `(euclidean_space (fin n), euclidean_half_space n)`, used as a
model for manifolds with boundary.
-/
def model_with_corners_euclidean_half_space (n : ‚Ñï) [has_zero (fin n)] :
  model_with_corners ‚Ñù (euclidean_space (fin n)) (euclidean_half_space n) :=
{ to_fun      := Œªx, x.val,
  inv_fun     := Œªx, ‚ü®Œªi, if h : i = 0 then max (x i) 0 else x i, by simp [le_refl]‚ü©,
  source      := univ,
  target      := range (Œªx : euclidean_half_space n, x.val),
  map_source' := Œªx hx, by simpa [-mem_range, mem_range_self] using x.property,
  map_target' := Œªx hx, mem_univ _,
  left_inv'   := Œª‚ü®xval, xprop‚ü© hx, begin
    rw subtype.mk_eq_mk,
    ext1 i,
    by_cases hi : i = 0,
    { rw hi, simp [hi, xprop] },
    { simp [hi] }
  end,
  right_inv'  := Œªx hx, begin
    simp [range_half_space] at hx,
    ext1 i,
    by_cases hi : i = 0,
    { rw hi, simp [hi, hx] },
    { simp [hi] }
  end,
  source_eq    := rfl,
  unique_diff' := begin
    /- To check that the half-space has the unique differentiability property, we use the criterion
    `unique_diff_on_convex`: it suffices to check that it is convex and with nonempty interior. -/
    rw range_half_space,
    apply unique_diff_on_convex,
    show convex {y : euclidean_space (fin n) | 0 ‚â§ y 0},
    { assume x y hx hy a b ha hb hab,
      simpa using add_le_add (mul_nonneg ha hx) (mul_nonneg hb hy) },
    show (interior {y : euclidean_space (fin n) | 0 ‚â§ y 0}).nonempty,
    { use (Œªi, 1),
      rw mem_interior,
      refine ‚ü®(pi (univ : set (fin n)) (Œªi, (Ioi 0 : set ‚Ñù))), _,
        is_open_set_pi finite_univ (Œªa ha, is_open_Ioi), _‚ü©,
      { assume x hx,
        simp [pi] at hx,
        exact le_of_lt (hx 0) },
      { simp only [pi, forall_prop_of_true, mem_univ, mem_Ioi],
        assume i,
        exact zero_lt_one } }
  end,
  continuous_to_fun  := continuous_subtype_val,
  continuous_inv_fun := begin
    apply continuous_subtype_mk,
    apply continuous_pi,
    assume i,
    by_cases h : i = 0,
    { rw h,
      simp only [dif_pos],
      have : continuous (Œªx:‚Ñù, max x 0) := continuous_id.max continuous_const,
      exact this.comp (continuous_apply 0) },
    { simp [h],
      exact continuous_apply i }
  end }

/--
Definition of the model with corners `(euclidean_space (fin n), euclidean_quadrant n)`, used as a
model for manifolds with corners -/
def model_with_corners_euclidean_quadrant (n : ‚Ñï) :
  model_with_corners ‚Ñù (euclidean_space (fin n)) (euclidean_quadrant n) :=
{ to_fun      := Œªx, x.val,
  inv_fun     := Œªx, ‚ü®Œªi, max (x i) 0, Œªi, by simp [le_refl]‚ü©,
  source      := univ,
  target      := range (Œªx : euclidean_quadrant n, x.val),
  map_source' := Œªx hx, by simpa [-mem_range, mem_range_self] using x.property,
  map_target' := Œªx hx, mem_univ _,
  left_inv'   := Œª‚ü®xval, xprop‚ü© hx, begin
    rw subtype.mk_eq_mk,
    ext1 i,
    simp [xprop i]
  end,
  right_inv' := Œªx hx, begin
    rw range_quadrant at hx,
    ext1 i,
    simp [hx i]
  end,
  source_eq    := rfl,
  unique_diff' := begin
    /- To check that the quadrant has the unique differentiability property, we use the criterion
    `unique_diff_on_convex`: it suffices to check that it is convex and with nonempty interior. -/
    rw range_quadrant,
    apply unique_diff_on_convex,
    show convex {y : euclidean_space (fin n) | ‚àÄ (i : fin n), 0 ‚â§ y i},
    { assume x y hx hy a b ha hb hab i,
      simpa using add_le_add (mul_nonneg ha (hx i)) (mul_nonneg hb (hy i)) },
    show (interior {y : euclidean_space (fin n) | ‚àÄ (i : fin n), 0 ‚â§ y i}).nonempty,
    { use (Œªi, 1),
      rw mem_interior,
      refine ‚ü®(pi (univ : set (fin n)) (Œªi, (Ioi 0 : set ‚Ñù))), _,
        is_open_set_pi finite_univ (Œªa ha, is_open_Ioi), _‚ü©,
      { assume x hx i,
        simp [pi] at hx,
        exact le_of_lt (hx i) },
      { simp only [pi, forall_prop_of_true, mem_univ, mem_Ioi],
        assume i,
        exact zero_lt_one } }
  end,
  continuous_to_fun  := continuous_subtype_val,
  continuous_inv_fun := begin
    apply continuous_subtype_mk,
    apply continuous_pi,
    assume i,
    have : continuous (Œªx:‚Ñù, max x 0) := continuous.max continuous_id continuous_const,
    exact this.comp (continuous_apply i)
  end }

localized "notation `ùì° `n := model_with_corners_self ‚Ñù (euclidean_space (fin n))" in manifold
localized "notation `ùì°‚àÇ `n := model_with_corners_euclidean_half_space n" in manifold


/--
The left chart for the topological space `[x, y]`, defined on `[x,y)` and sending `x` to `0` in
`euclidean_half_space 1`.
-/
def Icc_left_chart (x y : ‚Ñù) [fact (x < y)] :
  local_homeomorph (Icc x y) (euclidean_half_space 1) :=
{ source      := {z : Icc x y | z.val < y},
  target      := {z : euclidean_half_space 1 | z.val 0 < y - x},
  to_fun      := Œª(z : Icc x y), ‚ü®Œªi, z.val - x, sub_nonneg.mpr z.property.1‚ü©,
  inv_fun     := Œªz, ‚ü®min (z.val 0 + x) y, by simp [le_refl, z.property, le_of_lt ‚Äπx < y‚Ä∫]‚ü©,
  map_source' := by simp,
  map_target' := by { simp, assume z hz, left, dsimp at hz, linarith },
  left_inv'   := by { rintros ‚ü®z, hz‚ü© h'z, simp at hz h'z, simp [hz, h'z] },
  right_inv'  := begin
    rintros ‚ü®z, hz‚ü© h'z,
    rw subtype.mk_eq_mk,
    funext,
    dsimp at hz h'z,
    have A : x + z 0 ‚â§ y, by linarith,
    rw subsingleton.elim i 0,
    simp [A, add_comm],
  end,
  open_source := begin
    have : is_open {z : ‚Ñù | z < y} := is_open_Iio,
    exact continuous_subtype_val _ this
  end,
  open_target := begin
    have : is_open {z : ‚Ñù | z < y - x} := is_open_Iio,
    have : is_open {z : euclidean_space (fin 1) | z 0 < y - x} :=
      @continuous_apply (fin 1) (Œª _, ‚Ñù) _ 0 _ this,
    exact continuous_subtype_val _ this
  end,
  continuous_to_fun := begin
    apply continuous.continuous_on,
    apply continuous_subtype_mk,
    have : continuous (Œª (z : ‚Ñù) (i : fin 1), z - x) :=
      continuous.sub (continuous_pi $ Œªi, continuous_id) continuous_const,
    exact this.comp continuous_subtype_val,
  end,
  continuous_inv_fun := begin
    apply continuous.continuous_on,
    apply continuous_subtype_mk,
    have A : continuous (Œª z : ‚Ñù, min (z + x) y) :=
      (continuous_id.add continuous_const).min continuous_const,
    have B : continuous (Œªz : euclidean_space (fin 1), z 0) := continuous_apply 0,
    exact (A.comp B).comp continuous_subtype_val
  end }

/--
The right chart for the topological space `[x, y]`, defined on `(x,y]` and sending `y` to `0` in
`euclidean_half_space 1`.
-/
def Icc_right_chart (x y : ‚Ñù) [fact (x < y)] :
  local_homeomorph (Icc x y) (euclidean_half_space 1) :=
{ source      := {z : Icc x y | x < z.val},
  target      := {z : euclidean_half_space 1 | z.val 0 < y - x},
  to_fun      := Œª(z : Icc x y), ‚ü®Œªi, y - z.val, sub_nonneg.mpr z.property.2‚ü©,
  inv_fun     := Œªz,
    ‚ü®max (y - z.val 0) x, by simp [le_refl, z.property, le_of_lt ‚Äπx < y‚Ä∫, sub_eq_add_neg]‚ü©,
  map_source' := by simp,
  map_target' := by { simp, assume z hz, left, dsimp at hz, linarith },
  left_inv'   := by { rintros ‚ü®z, hz‚ü© h'z, simp at hz h'z, simp [hz, h'z, sub_eq_add_neg] },
  right_inv'  := begin
    rintros ‚ü®z, hz‚ü© h'z,
    rw subtype.mk_eq_mk,
    funext,
    dsimp at hz h'z,
    have A : x ‚â§ y - z 0, by linarith,
    rw subsingleton.elim i 0,
    simp [A, sub_sub_cancel],
  end,
  open_source := begin
    have : is_open {z : ‚Ñù | x < z} := is_open_Ioi,
    exact continuous_subtype_val _ this
  end,
  open_target := begin
    have : is_open {z : ‚Ñù | z < y - x} := is_open_Iio,
    have : is_open {z : euclidean_space (fin 1) | z 0 < y - x} :=
      @continuous_apply (fin 1) (Œª _, ‚Ñù) _ 0 _ this,
    exact continuous_subtype_val _ this
  end,
  continuous_to_fun := begin
    apply continuous.continuous_on,
    apply continuous_subtype_mk,
    have : continuous (Œª (z : ‚Ñù) (i : fin 1), y - z) :=
      continuous_const.sub (continuous_pi (Œªi, continuous_id)),
    exact this.comp continuous_subtype_val,
  end,
  continuous_inv_fun := begin
    apply continuous.continuous_on,
    apply continuous_subtype_mk,
    have A : continuous (Œª z : ‚Ñù, max (y - z) x) :=
      (continuous_const.sub continuous_id).max continuous_const,
    have B : continuous (Œªz : euclidean_space (fin 1), z 0) := continuous_apply 0,
    exact (A.comp B).comp continuous_subtype_val
  end }

/--
Manifold with boundary structure on `[x, y]`, using only two charts.
-/
instance Icc_manifold (x y : ‚Ñù) [fact (x < y)] : manifold (euclidean_half_space 1) (Icc x y) :=
{ atlas := {Icc_left_chart x y, Icc_right_chart x y},
  chart_at := Œªz, if z.val < y then Icc_left_chart x y else Icc_right_chart x y,
  mem_chart_source := Œªz, begin
    by_cases h' : z.val < y,
    { simp only [h', if_true],
      exact h' },
    { simp only [h', if_false],
      apply lt_of_lt_of_le ‚Äπx < y‚Ä∫,
      simpa using h' }
  end,
  chart_mem_atlas := Œªz, by { by_cases h' : z.val < y; simp [h'] } }

/--
The manifold structure on `[x, y]` is smooth.
-/
instance Icc_smooth_manifold (x y : ‚Ñù) [fact (x < y)] :
  smooth_manifold_with_corners (ùì°‚àÇ 1) (Icc x y) :=
begin
  have M : times_cont_diff_on ‚Ñù ‚ä§ (Œªz : euclidean_space (fin 1), - z + (Œªi, y - x)) univ,
  { rw times_cont_diff_on_univ,
    exact times_cont_diff_id.neg.add times_cont_diff_const  },
  haveI : has_groupoid (Icc x y)
          (times_cont_diff_groupoid ‚ä§ (model_with_corners_euclidean_half_space 1)) :=
  begin
    apply has_groupoid_of_pregroupoid,
    assume e e' he he',
    simp [atlas] at he he',
    /- We need to check that any composition of two charts gives a `C^‚àû` function. Each chart can be
    either the left chart or the right chart, leaving 4 possibilities that we handle successively.
    -/
    rcases he with rfl | rfl; rcases he' with rfl | rfl,
    { -- `e = left chart`, `e' = left chart`
      refine ((mem_groupoid_of_pregroupoid _ _).mpr _).1,
      exact symm_trans_mem_times_cont_diff_groupoid _ _ _ },
    { -- `e = left chart`, `e' = right chart`
      apply M.congr_mono _ (subset_univ _),
      assume z hz,
      simp [-mem_range, range_half_space, model_with_corners_euclidean_half_space,
            local_equiv.trans_source, Icc_left_chart, Icc_right_chart] at hz,
      have A : 0 ‚â§ z 0 := hz.2,
      have B : z 0 + x ‚â§ y, by { have := hz.1.1.1, linarith },
      ext i,
      rw subsingleton.elim i 0,
      simp [model_with_corners_euclidean_half_space, Icc_left_chart, Icc_right_chart, A, B],
      ring },
    { -- `e = right chart`, `e' = left chart`
      apply M.congr_mono _ (subset_univ _),
      assume z hz,
      simp [-mem_range, range_half_space, model_with_corners_euclidean_half_space,
            local_equiv.trans_source, Icc_left_chart, Icc_right_chart] at hz,
      have A : 0 ‚â§ z 0 := hz.2,
      have B : x ‚â§ y - z 0, by { have := hz.1.1.1, dsimp at this, linarith },
      ext i,
      rw subsingleton.elim i 0,
      simp [model_with_corners_euclidean_half_space, Icc_left_chart, Icc_right_chart, A, B],
      ring },
    { -- `e = right chart`, `e' = right chart`
      refine ((mem_groupoid_of_pregroupoid _ _).mpr _).1,
      exact symm_trans_mem_times_cont_diff_groupoid _ _ _ }
  end,
  constructor
end
